// separate Omnisight file, include if used

Class Omnisight : Inventory
{
	Mixin SWWMOverlapPickupSound;
	Mixin SWWMUseToPickup;
	Mixin SWWMRespawn;

	override bool Use( bool pickup )
	{
		if ( !level.allmap )
		{
			if ( Owner.player == players[consoleplayer] )
				Owner.A_StartSound("powerup/omnisight",CHAN_ITEMEXTRA,CHANF_OVERLAP);
			if ( Owner is 'SWWMPlayer' )
				SWWMPlayer(Owner).lastbump *= 1.1;
			level.allmap = true;
			SWWMUtility.SpawnExitMarkers();
		}
		// not used up, must be kept so the hud can detect it in order to amplify minimap range
		return false;
	}
	override bool HandlePickup( Inventory item )
	{
		if ( item.GetClass() == GetClass() )
		{
			item.bPickupGood = true;
			return true;
		}
		return false;
	}
	Default
	{
		Tag "$T_OMNISIGHT";
		Inventory.Icon "graphics/HUD/Icons/I_Omnisight.png";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "$I_OMNISIGHT";
		Inventory.MaxAmount 1;
		Inventory.InterHubAmount 0;
		Inventory.PickupFlash "SWWMCyanPickupFlash";
		+INVENTORY.ALWAYSPICKUP;
		+INVENTORY.AUTOACTIVATE;
		+COUNTITEM;
		+INVENTORY.BIGPOWERUP;
		+INVENTORY.UNDROPPABLE;
		+INVENTORY.UNTOSSABLE;
		+FLOATBOB;
		FloatBobStrength 0.25;
		Radius 6;
		Height 26;
	}
	States
	{
	Spawn:
		XZW1 A -1;
		Stop;
	}
}
