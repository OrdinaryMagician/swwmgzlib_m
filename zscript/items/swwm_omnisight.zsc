// separate Omnisight file, include if used

Class Omnisight : Inventory
{
	Mixin SWWMOverlapPickupSound;
	Mixin SWWMUseToPickup;
	Mixin SWWMRespawn;

	override bool TryPickup( in out Actor toucher )
	{
		if ( !level.allmap )
		{
			Actor rt = toucher;
			if ( toucher.player ) rt = toucher.player.mo;
			if ( rt.player == players[consoleplayer] )
				rt.A_StartSound("powerup/omnisight",CHAN_ITEMEXTRA,CHANF_OVERLAP);
			if ( rt is 'SWWMPlayer' )
				SWWMPlayer(rt).lastbump *= 1.1;
			level.allmap = true;
			SWWMUtility.SpawnExitMarkers();
		}
		GoAwayAndDie();
		return true;
	}
	Default
	{
		Tag "$T_OMNISIGHT";
		Inventory.Icon "graphics/HUD/Icons/I_Omnisight.png";
		Inventory.PickupSound "misc/p_pkup";
		Inventory.PickupMessage "$I_OMNISIGHT";
		Inventory.PickupFlash "SWWMCyanPickupFlash";
		+INVENTORY.ALWAYSPICKUP;
		+COUNTITEM;
		+INVENTORY.BIGPOWERUP;
		+FLOATBOB;
		+NOTDMATCH;
		FloatBobStrength 0.25;
		Radius 6;
		Height 26;
	}
	States
	{
	Spawn:
		XZW1 A -1;
		Stop;
	}
}
