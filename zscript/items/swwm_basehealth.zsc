// Base class for all SWWM Health
Class SWWMHealth : Inventory abstract
{
	Mixin SWWMUseToPickup;
	Mixin SWWMOverlapPickupSound;
	Mixin SWWMRespawn;
	Mixin SWWMPickupGlow;
	Mixin SWWMCountNoPickup;

	// can't use the Health class for whatever reason
	// nice parser you got there I guess?
	Class<Inventory> giveme;
	int giveamt;

	Property GiveHealth : giveme;
	Property GiveAmount : giveamt;

	override bool Use( bool pickup )
	{
		// healing items can't be used if at max health unless it's bonus health
		if ( (MaxAmount > 0) && (Owner.Health >= GetDefaultByType(giveme).MaxAmount) ) return false;
		// healing items won't get auto-used on pickup if their healing could "be wasted", unless they're powerup or bonus health
		if ( pickup && !bBIGPOWERUP && (MaxAmount > 0) && (Owner.Health+giveamt > GetDefaultByType(giveme).MaxAmount) ) return false;
		if ( pickup && ((Owner.player == players[consoleplayer]) || bBIGPOWERUP) )
			Owner.A_StartSound(UseSound,CHAN_ITEMEXTRA,CHANF_OVERLAP);
		SWWMHandler.HealthFlash(Owner.PlayerNumber());
		Owner.GiveInventory(giveme,giveamt);
		return true;
	}

	Default
	{
		+INVENTORY.INVBAR;
		+INVENTORY.ISHEALTH;
		+INVENTORY.AUTOACTIVATE;
		Inventory.MaxAmount 5;
		Inventory.InterHubAmount 5;
		Inventory.UseSound "misc/health_pkup";
		Inventory.PickupFlash "SWWMBluePickupFlash";
		+FLOATBOB;
		+DONTGIB;
		FloatBobStrength 0.25;
	}
}
